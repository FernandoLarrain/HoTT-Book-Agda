{-# OPTIONS --without-K --exact-split --rewriting #-}

open import Ch1.Type-theory
open import Ch2.Homotopy-type-theory
open import Ch3.Sets-and-logic

module Ch6.2-Induction-pples-and-dependent-paths where


-- Equation 6.2.2 (New notation for the type of dependent paths between two points, a.k.a. "PathOvers").

{- Note: up to this point in the book, no official definition of PathOver was required; transport and _â‰¡_ sufficed. -} 

PathOver : {X : ğ“¤ Ì‡} (P : X â†’ ğ“¥ Ì‡) {x y : X} (p : x â‰¡ y) (u : P x) (v : P y) â†’ ğ“¥ Ì‡
PathOver P p u v = transport P p u â‰¡ v

infix 0 PathOver

syntax PathOver P p u v = u â‰¡ v [ P â†“ p ]


-- The rewrite relation _â†¦_.

{- Agda has no native support for HITs, so we have to postulate them. To obtain definitonal equality for point constructors, we extend Agda's evaluation relation with new computation rules defined via _â†¦_ -}

postulate

  _â†¦_ : {A : ğ“¤ Ì‡} â†’ A â†’ A â†’ ğ“¤ Ì‡

infix 0 _â†¦_

{-# BUILTIN REWRITE _â†¦_ #-} 


-- The Circle, ğ•ŠÂ¹.

postulate

  -- (i) Type formation rule

  ğ•ŠÂ¹ : ğ“¤â‚€ Ì‡

  -- (ii) Constructors
  
  baseÂ¹ : ğ•ŠÂ¹ -- point constructor
  loop : baseÂ¹ â‰¡ baseÂ¹ -- path constructor

module _ (P : ğ•ŠÂ¹ â†’ ğ“¤ Ì‡) (b : P baseÂ¹) (l : b â‰¡ b [ P â†“ loop ]) where

  postulate

    -- (iii) Induction principle

    ğ•ŠÂ¹-ind : Î  P
  
    -- (iv) Computation rules

    baseÂ¹-Î² : ğ•ŠÂ¹-ind baseÂ¹ â†¦ b

    {-# REWRITE baseÂ¹-Î² #-}

    loop-Î² : apd ğ•ŠÂ¹-ind loop â‰¡ l


-- Lemma 6.2.5 (Recursion principle for ğ•ŠÂ¹).

module _ {A : ğ“¤ Ì‡} (a : A) (p : a â‰¡ a) where

  ğ•ŠÂ¹-rec : ğ•ŠÂ¹ â†’ A
  ğ•ŠÂ¹-rec = ğ•ŠÂ¹-ind (Î» x â†’ A)  a (transportconst A loop a âˆ™ p)
  
  baseÂ¹-Î²' : ğ•ŠÂ¹-rec baseÂ¹ â‰¡ a
  baseÂ¹-Î²' = refl _

  loop-Î²' : ap ğ•ŠÂ¹-rec loop â‰¡ p
  loop-Î²' = âˆ™â‚—-inv _ (ap ğ•ŠÂ¹-rec loop) p (apd-const A ğ•ŠÂ¹-rec loop â»Â¹ âˆ™ loop-Î² (Î» x â†’ A) a (transportconst A loop a âˆ™ p))
  

-- Lemma 6.2.8 (Uniqueness principle for ğ•ŠÂ¹)

ğ•ŠÂ¹-Î·' : {A : ğ“¤ Ì‡} (f g : ğ•ŠÂ¹ â†’ A) (p : f baseÂ¹ â‰¡ g baseÂ¹) (q : ap f loop â‰¡ ap g loop [ (Î» - â†’ - â‰¡ -) â†“ p ]) â†’ f âˆ¼ g
ğ•ŠÂ¹-Î·' {A = A} f g p q = ğ•ŠÂ¹-ind (Î» x â†’ f x â‰¡ g x) p (
  transport-funval-â‰¡ f g loop p âˆ™
  ap (Î» - â†’ ap f loop â»Â¹ âˆ™ p âˆ™ -) (q â»Â¹ âˆ™  transport-loop p (ap f loop)) âˆ™
  âˆ™-assoc _ _ _ â»Â¹ âˆ™
  ap (ap f loop â»Â¹ âˆ™_) (âˆ™-assoc _ _ _) âˆ™
  ap (Î» - â†’ ap f loop â»Â¹ âˆ™ (- âˆ™ p)) (âˆ™-assoc _ _ _) âˆ™
  ap (Î» - â†’ ap f loop â»Â¹ âˆ™ (- âˆ™ p)) (ap (_âˆ™ ap f loop) (rinv _) âˆ™ (lu _ â»Â¹)) âˆ™    âˆ™-assoc _ _ _ âˆ™
  ap (_âˆ™ p) (linv _) âˆ™
  lu _ â»Â¹
  )


-- Lemma 6.2.9 (UMP of ğ•ŠÂ¹).

UMP-ğ•ŠÂ¹ : (A : ğ“¤ Ì‡) â†’ (ğ•ŠÂ¹ â†’ A) â‰ƒ (Î£ x ê‰ A , x â‰¡ x)
UMP-ğ•ŠÂ¹ A =
  (Î» f â†’ (f baseÂ¹ , ap f loop)) ,
  (qinv-to-isequiv (
    Î£-induction (ğ•ŠÂ¹-rec {A = A}) ,
    Î£-induction (Î» a l â†’ dpair-â‰¡ (refl _ , loop-Î²' a l)) ,
    Î» f â†’ funext _ _ (ğ•ŠÂ¹-Î·' _ _ (refl _) (loop-Î²' (f baseÂ¹) (ap f loop))) 
    )
  )


    

  

