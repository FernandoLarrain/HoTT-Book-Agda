{-# OPTIONS --without-K --exact-split --safe #-}

open import Ch1.Type-theory

module Ch2.1-Types-are-higher-groupoids where


-- Lemma 2.1.1 (Inverse path).

_â»Â¹ : {A : ğ“¤ Ì‡} {x y : A} â†’ x â‰¡ y â†’ y â‰¡ x
refl x â»Â¹ = refl x

infix 40 _â»Â¹


-- Lemma  2.1.2 (Path concatenation).

_âˆ™_ : {A : ğ“¤ Ì‡} {x y z : A} â†’ x â‰¡ y â†’ y â‰¡ z â†’ x â‰¡ z
refl x âˆ™ refl .x = refl x

infixl 30 _âˆ™_


-- Equational reasoning with _â‰¡_

_â‰¡âŸ¨_âŸ©_ : {X : ğ“¤ Ì‡} (x : X) {y z : X} â†’ x â‰¡ y â†’ y â‰¡ z â†’ x â‰¡ z
x â‰¡âŸ¨ p âŸ© q = p âˆ™ q

infixr 0 _â‰¡âŸ¨_âŸ©_

_âˆ : {X : ğ“¤ Ì‡} (x : X) â†’ x â‰¡ x
x âˆ = refl x

infix 1 _âˆ


-- Lemma 2.1.4 (Groupoid laws).

module _ {A : ğ“¤ Ì‡} {x y : A} where 

  ru : (p : x â‰¡ y) â†’ p â‰¡ p âˆ™ refl y
  ru (refl x) = refl (refl x)

  lu : (p : x â‰¡ y) â†’ p â‰¡ refl x âˆ™ p
  lu (refl x) = refl (refl x)

  rinv : (p : x â‰¡ y) â†’ p âˆ™ p â»Â¹ â‰¡ refl x
  rinv (refl x) = refl (refl x)

  linv : (p : x â‰¡ y) â†’ p â»Â¹ âˆ™ p â‰¡ refl y
  linv (refl x) = refl (refl x)

  â»Â¹-invol : (p : x â‰¡ y) â†’ (p â»Â¹) â»Â¹ â‰¡ p
  â»Â¹-invol (refl x) = refl (refl x)

âˆ™-assoc : {A : ğ“¤ Ì‡} {x y z w : A} (p : x â‰¡ y) (q : y â‰¡ z) (r : z â‰¡ w) â†’ p âˆ™ (q âˆ™ r) â‰¡ p âˆ™ q âˆ™ r
âˆ™-assoc (refl x) (refl x) (refl x) = refl (refl x)

-- A related law

distr : {A : ğ“¤ Ì‡} {x y z : A} (p : x â‰¡ y) (q : y â‰¡ z) â†’ (p âˆ™ q) â»Â¹ â‰¡ q â»Â¹ âˆ™ p â»Â¹
distr (refl _) (refl _) = refl _


-- Theorem 2.1.6 (Eckmann-Hilton).

module _ {A : ğ“¤ Ì‡} {a b c : A} where

  -- Whiskering operations

  _âˆ™áµ£_ : {p q : a â‰¡ b} (Î± : p â‰¡ q) (r : b â‰¡ c) â†’ p âˆ™ r â‰¡ q âˆ™ r
  _âˆ™áµ£_ {p} {q} Î± (refl b) = (ru p â»Â¹ âˆ™ Î±) âˆ™ ru q

  infix 30 _âˆ™áµ£_

  _âˆ™â‚—_ : (q : a â‰¡ b) {r s : b â‰¡ c} (Î² : r â‰¡ s) â†’ q âˆ™ r â‰¡ q âˆ™ s
  _âˆ™â‚—_ (refl b) {r} {s} Î² = (lu r â»Â¹ âˆ™ Î²) âˆ™ lu s

  infix 30 _âˆ™â‚—_

  -- Horizontal composition

  _âœ¦_ : {p q : a â‰¡ b} {r s : b â‰¡ c} (Î± : p â‰¡ q) (Î² : r â‰¡ s) â†’ p âˆ™ r â‰¡ q âˆ™ s
  _âœ¦_ {q = q} {r = r} Î± Î² = (Î± âˆ™áµ£ r) âˆ™ (q âˆ™â‚— Î²)

  infixl 30 _âœ¦_

  _âœ§_ : {p q : a â‰¡ b} {r s : b â‰¡ c} (Î± : p â‰¡ q) (Î² : r â‰¡ s) â†’ (p âˆ™ r) â‰¡ (q âˆ™ s)
  _âœ§_ {p = p} {s = s} Î± Î² = (p âˆ™â‚— Î²) âˆ™ (Î± âˆ™áµ£ s)

  infixl 30 _âœ§_

  hz-comp-agreement : {p q : a â‰¡ b} {r s : b â‰¡ c} (Î± : p â‰¡ q) (Î² : r â‰¡ s) â†’ (Î± âœ¦ Î²) â‰¡ (Î± âœ§ Î²)
  hz-comp-agreement {refl x} {refl x} {refl x} {refl x} (refl (refl x)) (refl (refl x)) = refl (refl (refl x))

  {- For the proof of theorem, see next section, after Lemma 2.2.1 (Action on paths). The book proves Eckmann-Hilton before defining action on paths, but it is not clear how to do so without doing a path induction or using one of the asymmetric definitions of _âˆ™_. -}


-- Definition 2.1.7 (pointed type).

_âŠ™ : (ğ“¤ : Universe) â†’ ğ“¤ âº Ì‡
ğ“¤ âŠ™ = Î£ A ê‰ ğ“¤ Ì‡ , A


-- Definition (based map).

MapâŠ™ : (A : ğ“¤ âŠ™) (B : ğ“¥ âŠ™) â†’ ğ“¤ âŠ” ğ“¥ Ì‡
MapâŠ™ (A , a) (B , b) = Î£ f ê‰ (A â†’ B) , f a â‰¡ b 


-- Definition 2.1.8 (n-fold iterated loop space).

Î© : ğ“¤ âŠ™ â†’ ğ“¤ âŠ™
Î© (A , a) = ((a â‰¡ a) , refl a)

Î©^ : â„• â†’ ğ“¤ âŠ™ â†’ ğ“¤ âŠ™
Î©^ zero (A , a) = A , a
Î©^ (succ n) (A , a) = Î©^ n ( Î© (A , a)) -- inner loop

Î©'^ : â„• â†’ ğ“¤ âŠ™ â†’ ğ“¤ âŠ™
Î©'^ zero (A , a) = (A , a)
Î©'^ (succ n) (A , a) = Î© (Î©^ n (A , a)) -- outer loop



